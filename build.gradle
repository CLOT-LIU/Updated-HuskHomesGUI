import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta16'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.ajoberstar.grgit' version '5.3.0'
}

group 'pro.obydux'
version "$ext.plugin_version"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.william278.net/releases' }
    maven { url 'https://repo.william278.net/snapshots' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:26.0.2'
    compileOnly 'net.william278.huskhomes:huskhomes-bukkit:4.9.7-d529f5f'
    compileOnly 'org.apache.commons:commons-text:1.13.1'
    compileOnly 'net.william278:minedown:1.8.2'
    compileOnly 'dev.dejvokep:boosted-yaml:1.3.7'

    implementation 'de.themoep:inventorygui:1.6.5-SNAPSHOT'
    implementation 'net.wesjd:anvilgui:1.10.5-SNAPSHOT'
    implementation 'net.william278:annotaml:2.0.7-4f14c61'
    implementation 'net.william278:desertwell:2.0.4'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.1'
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.release.set 17

processResources {
    filesNotMatching(['**/*.png', '**/*.svg']) {
        filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                tokens: rootProject.ext.properties
    }
}

shadowJar {
    relocate 'de.themoep.inventorygui', 'pro.obydux.huskhomes.gui.libraries.inventorygui'
    relocate 'net.wesjd.anvilgui', 'pro.obydux.huskhomes.gui.libraries.anvilgui'
    relocate 'net.william278.annotaml', 'pro.obydux.huskhomes.gui.libraries.annotaml'
    relocate 'net.william278.desertwell', 'pro.obydux.huskhomes.gui.libraries.desertwell'

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
}

jar {
    dependsOn('shadowJar')
}

test {
    useJUnitPlatform()
}

license {
    header = rootProject.file('HEADER')
    include '**/*.java'
    newLine = true
}

logger.lifecycle("Building HuskHomesGUI ${version} by William278 & Obydux")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // Get if there is a tag for this commit
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }

    // Otherwise, get the last commit hash and if it's a clean head
    if (grgit == null) {
        return '-' + System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return '-' + grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}
